apply plugin: 'com.android.application'

static def generateVersionCode() {
    def result = "git rev-list --all --count".execute().text.trim() //unix
    return result.toInteger()
}

def majorVersion = "4.1.0"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    // Download the latest data file on build
    def matchesUrl = "https://bravelocation.com/automation/feeds/matches.json"
    def matchesFile = new File('app/src/main/assets/matches.json')
    new URL(matchesUrl).withInputStream{ i -> matchesFile.withOutputStream{ it << i }}

    def gameScoreUrl = "https://bravelocation.com/automation/feeds/gamescore.json"
    def gameScoreFile = new File('app/src/main/assets/gamescore.json')
    new URL(gameScoreUrl).withInputStream{ i -> gameScoreFile.withOutputStream{ it << i }}

    defaultConfig {
        applicationId "com.bravelocation.yeltzlandnew"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode generateVersionCode()
        versionName "${majorVersion}.${generateVersionCode()}"

        // Enabling multidex support.
        multiDexEnabled true
    }
    buildTypes {
        debug {
            buildConfigField('String', "TwitterKey", yeltzland_twitter_key)
            buildConfigField('String', "TwitterSecret", yeltzland_twitter_secret)
        }
        release {
            buildConfigField('String', "TwitterKey", yeltzland_twitter_key)
            buildConfigField('String', "TwitterSecret", yeltzland_twitter_secret)

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'

    implementation(name:'tweet-ui-release', ext:'aar')
    implementation(name:'twitter-core-release', ext:'aar')

    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0-rc01' // Need the RC version to fix a bug in Android 21 in inflating WebView XML - https://stackoverflow.com/questions/59572065/error-inflating-class-android-webkit-webview-on-android-5

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.firebase:firebase-core:17.4.4'
    implementation 'com.google.firebase:firebase-messaging:20.2.3'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'androidx.webkit:webkit:1.2.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.squareup.picasso:picasso:2.71828'
}

apply plugin: 'com.google.gms.google-services'
